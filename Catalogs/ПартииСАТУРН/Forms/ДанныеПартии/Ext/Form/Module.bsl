#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ДанныеПартии") Тогда
		ЗаполнитьЗначенияСвойств(Партия, Параметры.ДанныеПартии);
	КонецЕсли;
	
	Если Параметры.Свойство("НередактируемыеРеквизиты") Тогда
		Для Каждого ИмяРеквизита Из Параметры.НередактируемыеРеквизиты Цикл
			Элементы[СтрШаблон("Партия%1", ИмяРеквизита)].ТолькоПросмотр = Истина;
		КонецЦикла;
	КонецЕсли;
	
	СобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ЗначениеЗаполнено(Партия.НомерПартии) Тогда
		ТекстСообщения = Нстр("ru = 'Поле ""Маркировка / № партии"" не заполнено'");
		ОбщегоНазначения.СообщитьПользователю(
			ТекстСообщения,,,
			"Партия.НомерПартии",
			Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеПартии = Новый Структура;
	ДанныеПартии.Вставить("НомерПартии",          Партия.НомерПартии);
	ДанныеПартии.Вставить("ТорговоеНаименование", Партия.ТорговоеНаименование);
	ДанныеПартии.Вставить("АртикулПроизводителя", Партия.АртикулПроизводителя);
	ДанныеПартии.Вставить("ДатаПроизводства",     Партия.ДатаПроизводства);
	ДанныеПартии.Вставить("СрокГодности",         Партия.СрокГодности);
	
	Закрыть(ДанныеПартии);
	
КонецПроцедуры

#КонецОбласти
