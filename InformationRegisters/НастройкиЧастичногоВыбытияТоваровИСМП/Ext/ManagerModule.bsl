#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьОписание(Номенклатура, Характеристика) Экспорт
	
	ВозвращаемоеЗначение = Новый  Структура;
	ВозвращаемоеЗначение.Вставить("НоменклатураНастроена",           Ложь);
	ВозвращаемоеЗначение.Вставить("НоменклатураЧастичногоВыбытия",   ИнтеграцияИС.ПустоеЗначениеОпределяемогоТипа("Номенклатура"));
	ВозвращаемоеЗначение.Вставить("ХарактеристикаЧастичногоВыбытия", ИнтеграцияИС.ПустоеЗначениеОпределяемогоТипа("ХарактеристикаНоменклатуры"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИСТИНА                                                                КАК НоменклатураНастроена,
		|	НастройкиЧастичногоВыбытияТоваровИСМП.НоменклатураЧастичногоВыбытия   КАК НоменклатураЧастичногоВыбытия,
		|	НастройкиЧастичногоВыбытияТоваровИСМП.ХарактеристикаЧастичногоВыбытия КАК ХарактеристикаЧастичногоВыбытия
		|ИЗ
		|	РегистрСведений.НастройкиЧастичногоВыбытияТоваровИСМП КАК НастройкиЧастичногоВыбытияТоваровИСМП
		|ГДЕ
		|	НастройкиЧастичногоВыбытияТоваровИСМП.Номенклатура = &Номенклатура
		|	И НастройкиЧастичногоВыбытияТоваровИСМП.Характеристика = &Характеристика";
	
	Запрос.УстановитьПараметр("Номенклатура",   Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ВозвращаемоеЗначение, ВыборкаДетальныеЗаписи);
	КонецЦикла;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ЭтоНастроенноеВыбытие(Номенклатура, Характеристика, НоменклатураЧастичногоВыбытия, ХарактеристикаЧастичногоВыбытия) Экспорт
	
	Описание = ПолучитьОписание(Номенклатура, Характеристика);
	
	Возврат (Описание.НоменклатураНастроена
		И Описание.НоменклатураЧастичногоВыбытия = НоменклатураЧастичногоВыбытия
		И Описание.ХарактеристикаЧастичногоВыбытия = ХарактеристикаЧастичногоВыбытия);
	
КонецФункции

Функция НастроеннаяНоменклатураЧастичногоВыбытия(СписокНоменклатуры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиЧастичногоВыбытияТоваровИСМП.Номенклатура                    КАК Номенклатура,
		|	НастройкиЧастичногоВыбытияТоваровИСМП.Характеристика                  КАК Характеристика,
		|	НастройкиЧастичногоВыбытияТоваровИСМП.НоменклатураЧастичногоВыбытия   КАК НоменклатураЧастичногоВыбытия,
		|	НастройкиЧастичногоВыбытияТоваровИСМП.ХарактеристикаЧастичногоВыбытия КАК ХарактеристикаЧастичногоВыбытия
		|ИЗ
		|	РегистрСведений.НастройкиЧастичногоВыбытияТоваровИСМП КАК НастройкиЧастичногоВыбытияТоваровИСМП
		|ГДЕ
		|	НастройкиЧастичногоВыбытияТоваровИСМП.Номенклатура В (&СписокНоменклатуры)";
	
	Запрос.УстановитьПараметр("СписокНоменклатуры", СписокНоменклатуры);
	
	УстановитьПривилегированныйРежим(Истина);
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	ТаблицаДанных.Индексы.Добавить("Номенклатура,Характеристика");
	
	Возврат ТаблицаДанных;
	
КонецФункции

#КонецОбласти

#КонецЕсли